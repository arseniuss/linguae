plugins {
    id 'com.android.library'
}

android {
    namespace 'lv.id.arseniuss.linguae'
    compileSdk 34

    defaultConfig {
        minSdk 24
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    libraryVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(generateVersionClass)
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.11.0'
}

configurations {
    jarDependencies {
        canBeResolved = true
        canBeConsumed = false
        extendsFrom implementation
    }
}

def versionName = rootProject.ext.versionName

task generateVersionClass {
    def outputDir = file("$buildDir/generated/source/version/main")
    def packagePath = "lv/id/arseniuss/linguae"

    outputs.dir(outputDir)

    doFirst {
        def versionFile = file("$outputDir/$packagePath/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package lv.id.arseniuss.linguae;

public class Version {
    public static final String VERSION = "${rootProject.ext.versionName}";
}
"""
    }
}

android.sourceSets.main.java.srcDirs += "$buildDir/generated/source/version/main"

configurations {
    jarDependencies {
        canBeResolved = true
        canBeConsumed = false
        extendsFrom implementation
    }
}

task createRunnableJar(type: Jar, dependsOn: [build, generateVersionClass]) {
    archiveBaseName.set("linguae-parser-${rootProject.ext.versionName}")

    from fileTree(dir: "$buildDir/intermediates/javac/", includes: ["**/*.class"])

    from android.sourceSets.main.java.srcDirs

    from {
        configurations.jarDependencies.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    exclude '**/R.class'
    exclude '**/R$*.class'
    exclude '**/BuildConfig.class'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'lv.id.arseniuss.linguae.Main',
                'Implementation-Version': rootProject.ext.versionName
    }
}
